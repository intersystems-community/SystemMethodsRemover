<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="SMR.Main">
<Description>
Utility to convert 2016.1 System methods calls into 2016.2 compliant code.</Description>
<Abstract>1</Abstract>
<TimeCreated>64190,49345.679318</TimeCreated>

<Method name="RemoveFromAllClasses">
<Description><![CDATA[
Entry point. Converts all user classes.
Params: Replace - what to replace $ with. Capitalize - capitalize the letter after $.
For example, with defaults: {}.$fromJSON -> {}.%FromJSON
w $System.Status.GetErrorText(##class(SMR.Main).RemoveFromAllClasses())]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Replace:%String="%",Capitalize:%Boolean=$$$YES</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim sc As %Status = $$$OK
	set sc = $System.OBJ.GetClassList(.Classes, "/application=0 /system=0 /percent=0 /mapped=0")
	Quit:$$$ISERR(sc)
	Set sc = ..RemoveFromClassLocal(.Classes, Replace, Capitalize)
	Quit sc
]]></Implementation>
</Method>

<Method name="RemoveFromSubclassesOf">
<Description><![CDATA[
Entry point. Converts all subclasses Of class (and class itself).
Params: Replace - what to replace $ with. Capitalize - capitalize the letter after $.
For example, with defaults: {}.$fromJSON -> {}.%FromJSON
w $System.Status.GetErrorText(##class(SMR.Main).RemoveFromSubclassesOf())]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Class:%String,Replace:%String="%",Capitalize:%Boolean=$$$YES</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim sc As %Status = $$$OK
	Set RS = ##class(%ResultSet).%New("%Dictionary.ClassDefinition:SubclassOf")
	Set sc = RS.%Execute(Class)
	Quit:$$$ISERR(sc) 
	
	Set Classes(Class) = ""

	While (RS.%Next(.sc)) {
		Quit:$$$ISERR(sc)		
		Set Classes(RS.Data("Name")) = ""
	}
	
	Set sc = ..RemoveFromClassLocal(.Classes, Replace, Capitalize)
	Quit sc
]]></Implementation>
</Method>

<Method name="RemoveFromMatchingClasses">
<Description><![CDATA[
Entry point. Converts all classes matching Mask pattern .
Params: Mask - pattern, value for LIKE SQL query. Replace - what to replace $ with. Capitalize - capitalize the letter after $.
For example, with defaults: {}.$fromJSON -> {}.%FromJSON
w $System.Status.GetErrorText(##class(SMR.Main).RemoveFromMatchingClasses())]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Mask:%String,Replace:%String="%",Capitalize:%Boolean=$$$YES</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim sc As %Status = $$$OK
	
	Set SQL = "SELECT ID FROM %Dictionary.ClassDefinition Where ID LIKE ?"
	Set ST = ##class(%SQL.Statement).%New()
	Set sc = ST.%Prepare(SQL)
	Quit:$$$ISERR(sc) sc
	
	#Dim RS As %SQL.StatementResult
	Set RS = ST.%Execute(Mask)
	
	While RS.%Next() {
		Set Classes(RS.%GetData(1)) = ""
	}
	
	Set sc = ..RemoveFromClassLocal(.Classes, Replace, Capitalize)
	Quit sc
]]></Implementation>
</Method>

<Method name="RemoveFromClassLocal">
<Description>
Iterates over Classes local and converts them.
w $System.Status.GetErrorText(##class(SMR.Main).RemoveFromClassLocal())</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&Classes:%String,Replace:%String="%",Capitalize:%Boolean=$$$YES]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim sc,sc1 As %Status = $$$OK
	Set Class = ""
	For { 
		Set Class = $Order(Classes(Class)) 
		Quit:Class=""
		Set sc1 = ..RemoveFromClass(Class, Replace, Capitalize)
		Set sc = $$$ADDSC(sc, sc1)		
	}
	Quit sc
]]></Implementation>
</Method>

<Method name="RemoveFromClass">
<Description><![CDATA[
Convert one class.
Params: Replace - what to replace $ with. Capitalize - capitalize the letter after $.
For example, with defaults: {}.$fromJSON -> {}.%FromJSON
w $System.Status.GetErrorText(##class(SMR.Main).RemoveFromClass())]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Class:%String,Replace:%String="%",Capitalize:%Boolean=$$$YES</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim sc As %Status = $$$OK
	
	Set sc = ##class(SMR.TextServices).GetTextAsString(, Class, .Text)
	Quit:$$$ISERR(sc) sc
	
	Set sc = ..ReplaceSystemCalls(.Text, Replace, Capitalize)
	Quit:$$$ISERR(sc) sc
	
	Set sc = ..ReplaceDynamicClasses(.Text)
	Quit:$$$ISERR(sc) sc
	
	Set sc = ##class(SMR.TextServices).SetTextFromString(, Class, .Text)	
	Quit sc
]]></Implementation>
</Method>

<Method name="ReplaceSystemCalls">
<Description><![CDATA[
Replace all .$ usage
For example, with defaults: {}.$fromJSON -> {}.%FromJSON
w $System.Status.GetErrorText(##class(SMR.Main).ReplaceText(.Text))]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&Text,Replace:%String="%",Capitalize:%Boolean=$$$YES]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set Position = 1
	For {
		Set Position = $Find(Text, ".$", Position)
		Quit:Position=0
		CONTINUE:$Extract(Text, Position+1)="$" // skip .$$$ macros
		Set:Capitalize $Extract(Text, Position) = $ZCVT($Extract(Text, Position), "U")
		Set $Extract(Text, Position-1) = Replace
	}
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="ReplaceDynamicClasses">
<Description>
Replace %Library.Object with %Library.DynamicObject etc...
w $System.Status.GetErrorText(##class(SMR.Main).ReplaceDynamicClasses(.Text))</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&Text]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim sc,sc1 As %Status = $$$OK
	Set ReplaceList = $LB(	$LB("%Library\.Object","%Library\.DynamicObject"),
							$LB("%Object","%DynamicObject"),
							$LB("%Library\.Array","%Library\.DynamicArray"),
							$LB("%Array","%DynamicArray"))
	For i=1:1:$LL(ReplaceList) {
		Set ReplacePair = $LG(ReplaceList, i)
		Set sc1 = ..ReplaceRegexp(.Text, $LG(ReplacePair, 1), $LG(ReplacePair, 2))
		Set sc = $$$ADDSC(sc, sc1)
	}
	Return sc
]]></Implementation>
</Method>

<Method name="ReplaceRegexp">
<Description>
Replaces all occurances of Pattern with ReplacePattern.
w $System.Status.GetErrorText(##class(SMR.Main).ReplaceRegexp(.Text))</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&Text,Pattern:%String="\.\$([a-zA-Z])",ReplacePattern:%String="\.\%\U$1"]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim Matcher As %Regex.Matcher = ##class(%Regex.Matcher).%New(Pattern, Text)
	Set Text = Matcher.ReplaceAll(ReplacePattern)
    Quit Matcher.Status
]]></Implementation>
</Method>
</Class>
</Export>
